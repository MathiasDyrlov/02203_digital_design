-- -----------------------------------------------------------------------------
--
--  Title      :  FSMD implementation of GCD
--             :
--  Developers :  Jens Sparsø, Rasmus Bo Sørensen and Mathias Møller Bruhn
--           :
--  Purpose    :  This is a FSMD (finite state machine with datapath) 
--             :  implementation the GCD circuit
--             :
--  Revision   :  02203 fall 2019 v.5.0
--
-- -----------------------------------------------------------------------------

library ieee;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;

entity gcd is
  port (clk : in std_logic;             -- The clock signal.
    reset : in  std_logic;              -- Reset the module.
    req   : in  std_logic;              -- Input operand / start computation.
    AB    : in  unsigned(15 downto 0);  -- The two operands.
    ack   : out std_logic;              -- Computation is complete.
    C     : out unsigned(15 downto 0)); -- The result.
end gcd;

architecture fsmd of gcd is

  type state_type is (Idle, LoadA, LoadB, Check, NewA, NewB, LoadC, Req0); -- Input your own state names

  signal reg_a, next_reg_a, next_reg_b, reg_b : unsigned(15 downto 0);

  signal state, next_state : state_type;


begin

  -- Combinatoriel logic

cl : process (req, ab, state, reg_a, reg_b)
begin
  next_state <= state;
  next_reg_a <= reg_a;
  next_reg_b <= reg_b;
  ack <= '0';
  C <= (others => 'Z');

  case (state) is

    when Idle =>
      if req = '1' then
        next_state <= LoadA;
      end if;

    -- State to load A, sets ack high
    when LoadA =>
      next_reg_a <= AB;
      ack <= '1';
      if req = '0' then
      next_state <= Req0;
      end if;

      
     -- State to set ack 0 and check for req again
     when Req0 =>
       ack <= '0';
       if req = '1' then
       next_state <= LoadB;
       end if;

    -- Load B value
    when LoadB =>
      next_reg_b <= AB;
      next_state <= Check;

    -- Check relationship between A and B
    when Check =>
      if reg_a = reg_b then
        next_state <= LoadC;
      elsif reg_a < reg_b then
        next_state <= NewB;
      else
        next_state <= NewA;
      end if;

    -- Subtract B from A
    when NewA =>
      next_reg_a <= reg_a - reg_b;
      next_state <= Check;

    -- Subtract A from B
    when NewB =>
      next_reg_b <= reg_b - reg_a;
      next_state <= Check;

    -- Drive C with GCD result and assert ack
    when LoadC =>
      C <= reg_a;
      ack <= '1';
      if req = '0' then
      next_state <= Idle;
      end if;

   

  end case;
end process cl;


  -- Registers

  seq : process (clk, reset)
  begin
    if reset = '1' then
        reg_a <= x"0000";
        reg_b <= x"0000";
        state <= idle;
    elsif clk' event and clk = '1' then
        reg_a <= next_reg_a;
        reg_b <= next_reg_b;
        state <= next_state;
    end if;

  end process seq;


end fsmd;
